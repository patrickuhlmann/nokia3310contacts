plugins {
    id 'java'
    id 'application'
    id "com.github.spotbugs" version "6.1.5"
    id 'pmd'
    id 'checkstyle'
    id "net.davidecavestro.gradle.jxr" version "0.2.1"
    id 'com.github.ben-manes.versions'  version '0.52.0' // see dependencyUpdates
}

group = 'ch.uhlme'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.googlecode.ez-vcard:ez-vcard:0.12.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

application {
    mainClassName = 'Application'
}

java {
    withJavadocJar()
    withSourcesJar()
}
jar.dependsOn javadocJar
jar.dependsOn sourcesJar

compileJava   {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.WARN
    
    manifest {
        attributes 'Main-Class': 'ch.uhlme.Application'
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

spotbugs {
    //noinspection GrFinalVariableAccess
    ignoreFailures = false
}

pmd {
    ignoreFailures = false
    toolVersion = "6.48.0"
}

spotbugsMain {
    reports {
 	// Only one report format is supported. Html is easier to read, so let's use that
    	// (xml is the one that's enabled by default)
    	xml.required = false
        html.required = true
        
    }
}

checkstyle {
    toolVersion '10.12.5'
    maxWarnings = 0
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named('dependencyUpdates') {
    doLast {
        dependencyUpdates {
            rejectVersionIf {
                isNonStable(it.candidate.version)
            }

            gradleReleaseChannel = "current"
            checkForGradleUpdate = true
            outputDir = "build/reports"
            reportfileName = "dependencyUpdates"
        }
    }
}
